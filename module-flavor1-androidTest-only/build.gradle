apply plugin: 'com.android.test' // A plugin used for test-only-modules
apply plugin: 'kotlin-android'

android {
    compileSdkVersion rootProject.ext.compileSdkVersion

    defaultConfig {
        minSdkVersion rootProject.ext.minSdkVersion
        targetSdkVersion rootProject.ext.targetSdkVersion

        // Only include the 'flavor1' product flavor from the 'defaultDimension' flavor dimension
        missingDimensionStrategy 'defaultDimension', 'flavor1'

        // The package name of the test app
        testApplicationId 'com.example.android.testing.blueprint.test'
        // The Instrumentation test runner used to run tests.
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
    }

    // Set the target app project. The module specified here should contain the production code
    // test should run against.
    targetProjectPath ':app'
}

dependencies {
    // Android Testing Support Library's runner and rules and hamcrest matchers
    implementation "androidx.test:runner:$rootProject.ext.runnerVersion"
    implementation "androidx.test:rules:$rootProject.ext.rulesVersion"
    implementation "org.hamcrest:hamcrest-core:$rootProject.ext.hamcrestVersion"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlinVersion"

    // https://github.com/android/android-test
    implementation "androidx.test:core-ktx:1.2.0"
    implementation "androidx.test.ext:junit:1.1.1"
    implementation "androidx.test.espresso:espresso-core:$rootProject.ext.espressoVersion"
    implementation "androidx.test.espresso:espresso-contrib:$rootProject.ext.espressoVersion"
    implementation "androidx.test.espresso:espresso-intents:$rootProject.ext.espressoVersion"
}
